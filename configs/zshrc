# ============================================================================
# OH MY ZSH CONFIGURATION
# ============================================================================

export ZSH="$HOME/.oh-my-zsh"
export ZSH_THEME="amuse"
export GCP_ZSH_THEME="dpoggi"

zstyle ':omz:update' mode disabled
export ZSH_DISABLE_COMPFIX=true

plugins=(git virtualenv helm gcloud kubectl kubectx kube-ps1)
if [[ -n "${ITERM_SESSION_ID:-}" ]]; then
  plugins+=(iterm2)
fi

source $ZSH/oh-my-zsh.sh

unalias omz-update-check 2>/dev/null
omz-update-check() {
  if command -v omz >/dev/null 2>&1; then
    omz update
  elif typeset -f upgrade_oh_my_zsh >/dev/null 2>&1; then
    upgrade_oh_my_zsh
  else
    echo "Oh My Zsh update command not found."
    return 1
  fi
}
alias omz-update='omz-update-check'

# ============================================================================
# ENVIRONMENT SETUP
# ============================================================================

# --- PATH ---
typeset -U path
path=(
  /opt/homebrew/bin
  /opt/homebrew/sbin
  /usr/bin
  /usr/local/bin
  /bin
  /usr/sbin
  /sbin
  /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/local/bin
  /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/bin
  /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/appleinternal/bin
  /Library/Apple/usr/bin
  /Applications/CyberArk\ EPM.app/Contents/MacOS
  /System/Cryptexes/App/usr/bin
  /Applications/iTerm.app/Contents/Resources/utilities
  /opt/homebrew/share/google-cloud-sdk/bin
  /opt/homebrew/Cellar/mysql-client/8.3.0/bin
  /opt/homebrew/opt/libpq/bin
  $HOME/Projects/dexcom-inc/sre/etc
  $HOME/Projects/clarkritchie/hot-garbage/bin
  $HOME/.local/bin
  $HOME/.rd/bin
  $path
)
path=(${^path}(N-/))
export PATH

# --- History ---
export HISTFILE="$HOME/.zsh_history"
export HISTSIZE=200000
export SAVEHIST=200000
setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_verify
setopt inc_append_history
setopt share_history

# --- Other Environment Variables ---
export TABSTOP=2
export CLOUDSDK_PYTHON_SITEPACKAGES=1

# --- rbenv ---
eval "$(rbenv init -)"

# ============================================================================
# KUBERNETES
# ============================================================================

alias k=kubectl
alias kcc="k config current-context"
alias kdp='f() { k describe pod $1 };f'
alias kevents='kubectl get events --sort-by=".metadata.creationTimestamp"'
alias klc="k config get-contexts"
alias kns="k get namespaces"
alias kuc="k config use-context"
alias kun='f() { k config set-context --current --namespace=$1 };f'

alias alt-kubeconfig='export KUBECONFIG=~/.kube/config-alt'
alias no-alt-kubeconfig='unset KUBECONFIG'

unalias kdec 2>/dev/null
kdec() {
  kubectl get secret $1 -o go-template='{{range $k,$v := .data}}{{printf "%s: " $k}}{{if not $v}}{{$v}}{{else}}{{$v | base64decode}}{{end}}{{"\n"}}{{end}}'
}

unalias kgo 2>/dev/null
kgo() {
  pod=$(kubectl get pods --no-headers | awk "NR==$1" | awk '{print $1}')
  echo "Getting a shell in $pod"
  kubectl exec --stdin --tty "$pod" -- /bin/bash
}

unalias krmreplicas 2>/dev/null
krmreplicas() {
  kubectl get rs | awk '$2==0 {print $1}' | xargs kubectl delete rs
}

unalias kubeconfig-status 2>/dev/null
kubeconfig-status() {
  local default_config="$HOME/.kube/config"
  if [[ -n "$KUBECONFIG" && "$KUBECONFIG" != "$default_config" ]]; then
    echo " ✴️ Alt KUBECONFIG"
  fi
}

# ============================================================================
# GIT & VERSION CONTROL
# ============================================================================

alias chart-bump="$HOME/Projects/clarkritchie/hot-garbage/bin/bump-helm-chart-version.zsh"
alias create-pr='create-pr.zsh'
alias current-tag='git describe --tags'
alias git-sha='git rev-parse --short HEAD'
alias git-tags='git fetch origin --tags && git tag --list --sort=creatordate | tail -1'

unalias delete-tag 2>/dev/null
delete-tag() {
  if [[ -z "$1" ]]; then
    echo "Usage: delete-tag <tag-name>"
    return 1
  fi
  local tag="$1"
  echo "Deleting tag '$tag' locally..."
  git tag -d "$tag" || echo "Failed to delete local tag (may not exist)"
  echo "Deleting tag '$tag' from remote..."
  git push --delete origin "$tag" || echo "Failed to delete remote tag (may not exist)"
  echo "✅ Tag deletion complete"
}

unalias prs 2>/dev/null
alias prs=prs.zsh

# reopen-pr 1234 y y - approve and auto-merge
# reopen-pr 1234 n y - don't approve, but auto-merge
# reopen-pr 1234 - prompt for both
# reopen-pr() {
#   if [[ -z "$1" ]]; then
#     echo "Usage: reopen-pr <pr-number> [approve:y/n] [auto-merge:y/n]"
#     return 1
#   fi
#   sleep=3s
#   local pr_number="$1"
#   local approve_pr="${2:-}"
#   local enable_auto="${3:-}"

#   # Prompt if not provided as arguments
#   if [[ -z "$approve_pr" ]]; then
#     echo -n "Approve this PR? [y/N]: "
#     read -r approve_pr
#   fi

#   if [[ -z "$enable_auto" ]]; then
#     echo -n "Enable auto-merge (squash)? [y/N]: "
#     read -r enable_auto
#   fi

#   echo "Closing PR #${pr_number}..."
#   gh pr close "$pr_number" || return 1
#   echo "Waiting $sleep seconds..."
#   sleep "$sleep"

#   echo "Reopening PR #${pr_number}..."
#   gh pr reopen "$pr_number" || return 1
#   echo "✅ PR #${pr_number} reopened - workflows should trigger now"

#   if [[ "$approve_pr" =~ ^[Yy]$ ]]; then
#     echo "Approving PR..."
#     gh pr review "$pr_number" --approve || return 1
#     echo "✅ PR approved"
#     sleep "$sleep"
#   fi

#   if [[ "$enable_auto" =~ ^[Yy]$ ]]; then
#     echo "Enabling auto-merge..."
#     gh pr merge "$pr_number" --auto --squash || return 1
#     echo "✅ Auto-merge enabled"
#     sleep "$sleep"
#   fi
# }
# alias re-open-pr=reopen-pr

unalias approve-merge-pr 2>/dev/null

# approve-merge-pr 1234 - approve and auto-merge without close/reopen
approve-merge-pr() {
  if [[ -z "$1" ]]; then
    echo "Usage: approve-merge-pr <pr-number>"
    return 1
  fi
  sleep=3s
  local pr_number="$1"

  echo "Approving PR #${pr_number}..."
  gh pr review "$pr_number" --approve || return 1
  echo "✅ PR approved"
  sleep "$sleep"

  echo "Enabling auto-merge..."
  gh pr merge "$pr_number" --auto --squash || return 1
  echo "✅ Auto-merge enabled"
}

unalias run-porcelain 2>/dev/null
run-porcelain() {
  echo "Untracked files (git status):"
  git status --porcelain
  echo "\nDry run (what would be deleted):"
  git clean -fdn
  echo -n "\nType 'yes' to delete these untracked files: "
  local confirm
  read -r confirm
  if [[ "$confirm" != "yes" ]]; then
    echo "Aborted."
    return 1
  fi
  git clean -fd
}

# ============================================================================
# SHELL/UTILITIES
# ============================================================================

alias ls='ls -lahp --color=auto'
# alias c='command'
alias rm-bak-files="find . -name '*.bak' -exec rm -f {} \;"

alias compile-on="sed -i '' '4,5 s/^/#/' Pulumi.yaml"
alias compile-off="sed -i '' '4,5 s/^#//' Pulumi.yaml"

unalias fix-whitespace 2>/dev/null
fix-whitespace() {
  local file_path="${1:-}"
  if [[ -z "$file_path" ]]; then
    echo "Usage: fix-whitespace <file>"
    return 2
  fi
  if [[ "$OSTYPE" == "darwin"* ]]; then
    sed -i '' 's/[[:space:]]*$//' "$file_path"
  else
    sed -i 's/[[:space:]]*$//' "$file_path"
  fi
}

alias yaml-az='yq e -i "sort_keys(..)" $1'

# ============================================================================
# ARGOCD
# ============================================================================

alias get-argo-secret='k get secret argocd-initial-admin-secret -n argocd -o jsonpath="{.data}" | jq -r '\''to_entries[] | "\(.key): \(.value | @base64d)"'\'

unalias argo-port-forward 2>/dev/null
argo-port-forward() {
  get-argo-secret
  RUNNING_PID=$(pgrep -f "kubectl port-forward svc/argocd-platform-server 8082:80 -n argocd")
  if [[ -n "$RUNNING_PID" ]]; then
    echo "Port-forward to localhost:8082 is already running with PID ${RUNNING_PID}. Exiting."
    return 1
  fi
  if [[ "$OSTYPE" == "darwin"* ]]; then
    k port-forward svc/argocd-platform-server 8082:80 -n argocd --context=$(kubectx -c)
  else
    k port-forward svc/argocd-platform-server 8082:80 -n argocd --context=$(kubectx -c)
  fi
}

# ============================================================================
# CONFIG MANAGEMENT
# ============================================================================

unalias new-zshrc 2>/dev/null
new-zshrc() {
  local src=~/Projects/clarkritchie/hot-garbage/configs/zshrc
  local dest=~/.zshrc
  local backup1='Library/Mobile Documents/iCloud~md~obsidian/Documents/Misc/obsidian-backup/Code/zshrc.zsh'
  echo "Installing new .zshrc"
  cp "$src" "$dest" && source "$dest" 2>/dev/null
  cp "$src" "${HOME}/$backup1"
}

unalias new-configs 2>/dev/null
new-configs() {
  python3 ~/Projects/clarkritchie/hot-garbage/bin/new-configs.py
}

# ============================================================================
# FZF INTEGRATION
# ============================================================================

unalias h 2>/dev/null
if [[ -f /opt/homebrew/bin/fzf ]]; then
  source <(fzf --zsh)
  alias h='selected=$(history -n 1 | fzf --tac --no-sort) && [[ -n "$selected" ]] && print -z "$selected"'
else
  alias h=history
fi

# ============================================================================
# PROMPT CUSTOMIZATION
# ============================================================================

unalias current-pulumi-stack 2>/dev/null
current-pulumi-stack() {
  local stack=$(pulumi stack --show-name 2>/dev/null)
  if [[ -n "$stack" ]]; then
    echo " [$stack]"
  fi
}

PROMPT='$(kube_ps1)$(kubeconfig-status)$(current-pulumi-stack) '$PROMPT

unalias ip 2>/dev/null
ip() {
  typeset -a ITERM_PROFILES=(
    "Fairyfloss"
    "ToyChest"
    "GruvboxDark"
    "Purple Rain"
    "FrontEndDelight"
    "Jackie Brown"
  )
  setopt KSH_ARRAYS 2>/dev/null
  local index=$((RANDOM % ${#ITERM_PROFILES[@]}))
  local random_profile=${ITERM_PROFILES[$index]}
  echo -e "\033]50;SetProfile=$random_profile\a"
  unsetopt KSH_ARRAYS 2>/dev/null
}

if [[ -o interactive && -n "${ITERM_SESSION_ID:-}" ]]; then
  ip
fi

# ============================================================================
# SOURCE EXTERNAL CONFIGS
# ============================================================================

for file in "$HOME/Projects/etc/api-keys.zshrc" "$HOME/Projects/etc/clark-more-zsh.zshrc" "$HOME/Projects/etc/cloudsql-aliases.zshrc"; do
  [[ -f $file ]] && source $file
done

# ============================================================================
# END
# ============================================================================
