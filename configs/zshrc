# --- Oh My Zsh & Theme Setup ---
export ZSH="$HOME/.oh-my-zsh"
# Themes: https://github.com/ohmyzsh/ohmyzsh/wiki/themes
ZSH_THEME="amuse"
export GCP_ZSH_THEME="dpoggi"

# Speed startup (avoid OMZ update checks / compaudit work)
zstyle ':omz:update' mode disabled
export ZSH_DISABLE_COMPFIX=true

# On-demand update check (since auto-update checks are disabled above)
omz-update-check() {
  if command -v omz >/dev/null 2>&1; then
    omz update
  elif typeset -f upgrade_oh_my_zsh >/dev/null 2>&1; then
    upgrade_oh_my_zsh
  else
    echo "Oh My Zsh update command not found."
    return 1
  fi
}
alias omz-update='omz-update-check'

plugins=(git virtualenv helm gcloud kubectl kubectx kube-ps1)
if [[ -n "${ITERM_SESSION_ID:-}" ]]; then
  plugins+=(iterm2)
fi
source $ZSH/oh-my-zsh.sh

# --- PATH Setup (using array for clarity) ---
typeset -U path
path=(
  /opt/homebrew/bin
  /opt/homebrew/sbin
  /usr/bin
  /usr/local/bin
  /bin
  /usr/sbin
  /sbin
  /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/local/bin
  /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/bin
  /var/run/com.apple.security.cryptexd/codex.system/bootstrap/usr/appleinternal/bin
  /Library/Apple/usr/bin
  /Applications/CyberArk\ EPM.app/Contents/MacOS
  /System/Cryptexes/App/usr/bin
  /Applications/iTerm.app/Contents/Resources/utilities
  # $HOME/.rd/bin
  /opt/homebrew/share/google-cloud-sdk/bin
  $HOME/Projects/dexcom-inc/sre/etc
  $HOME/Projects/clarkritchie/hot-garbage/bin
  # $(go env GOPATH)/bin
  /opt/homebrew/Cellar/mysql-client/8.3.0/bin
  /opt/homebrew/opt/libpq/bin
  $HOME/.local/bin
  $path
)

# Drop any PATH entries that don't exist on this machine (helps when sharing config across OSes)
path=(${^path}(N-/))
export PATH

# --- History (make recall/search great) ---
export HISTFILE="$HOME/.zsh_history"
export HISTSIZE=200000
export SAVEHIST=200000
setopt append_history
setopt extended_history
setopt hist_expire_dups_first
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_verify
setopt inc_append_history
setopt share_history

# --- Aliases ---
alias alt-kubeconfig='export KUBECONFIG=~/.kube/config-alt'
alias chart-bump="$HOME/Projects/clarkritchie/hot-garbage/bin/bump-helm-chart-version.zsh"
alias create-pr='create-pr.zsh'
alias current-tag='git describe --tags'
fix-whitespace() {
  local file_path="${1:-}"
  if [[ -z "$file_path" ]]; then
    echo "Usage: fix-whitespace <file>"
    return 2
  fi

  if [[ "$OSTYPE" == "darwin"* ]]; then
    sed -i '' 's/[[:space:]]*$//' "$file_path"
  else
    sed -i 's/[[:space:]]*$//' "$file_path"
  fi
}
alias get-argo-secret='k get secret argocd-initial-admin-secret -n argocd -o jsonpath="{.data}" | jq -r '\''to_entries[] | "\(.key): \(.value | @base64d)"'\'
alias git-sha='git rev-parse --short HEAD'
alias git-tags='git fetch origin --tags && git tag --list --sort=creatordate | tail -1'
alias go-database='cd ~/Projects/dexcom-inc/database'
alias go-sre='cd ~/Projects/dexcom-inc/sre'
alias go-sre-libs='cd ~/Projects/dexcom-inc/sre-libs'
alias h='history'
alias ls="ls -la --color"
alias no-alt-kubeconfig='unset KUBECONFIG'
alias rm-bak-files="find . -name '*.bak' -exec rm -f {} \;"

# Safer replacement for the old "run-porcelain" (which could delete the wrong things and breaks on spaces).
# Dry-run first, then require an explicit confirmation before deletion.
run-porcelain() {
  echo "Untracked files (git status):"
  git status --porcelain

  echo "\nDry run (what would be deleted):"
  git clean -fdn

  echo -n "\nType 'yes' to delete these untracked files: "
  local confirm
  read -r confirm
  if [[ "$confirm" != "yes" ]]; then
    echo "Aborted."
    return 1
  fi

  git clean -fd
}

# Aliases to comment or uncomment the lines that makes Pulumi re-compile the Go binary
alias compile-on="sed -i '' '4,5 s/^/#/' Pulumi.yaml"
alias compile-off="sed -i '' '4,5 s/^#//' Pulumi.yaml"

# --- Personal Functions ---
new-zshrc() {
  local src=~/Projects/clarkritchie/hot-garbage/configs/zshrc
  local dest=~/.zshrc
  local backup1='Library/Mobile Documents/iCloud~md~obsidian/Documents/Misc/obsidian-backup/Code/zshrc.zsh'
  echo "Installing new .zshrc"
  # redirect to silence command not found with path
  cp "$src" "$dest" && source "$dest" 2>/dev/null
  cp "$src" "${HOME}/$backup1"
}

new-configs() {
  python3 ~/Projects/clarkritchie/hot-garbage/bin/new-configs.py
}

# Kube PS1 prompt
# https://github.com/jonmosco/kube-ps1

# Declare a function to show the current Pulumi stack
current-pulumi-stack() {
  local stack=$(pulumi stack --show-name 2>/dev/null)
  if [[ -n "$stack" ]]; then
    echo " [$stack]"
  fi
}

# --- Kubernetes Shortcuts ---
alias k=kubectl
alias klc="k config get-contexts"
alias kcc="k config current-context"
alias kuc="k config use-context"
alias kns="k get namespaces"
alias kdp='f() { k describe pod $1 };f'
alias kun='f() { k config set-context --current --namespace=$1 };f'
alias kevents='kubectl get events --sort-by=".metadata.creationTimestamp"'

kgo() {
  pod=$(kubectl get pods --no-headers | awk "NR==$1" | awk '{print $1}')
  echo "Getting a shell in $pod"
  kubectl exec --stdin --tty "$pod" -- /bin/bash
}

krmreplicas() {
  kubectl get rs | awk '$2==0 {print $1}' | xargs kubectl delete rs
}

# Function to show KUBECONFIG status in prompt if $KUBECONFIG is currently pointed elsewhere
kubeconfig-status() {
  local default_config="$HOME/.kube/config"
  if [[ -n "$KUBECONFIG" && "$KUBECONFIG" != "$default_config" ]]; then
    echo " ✴️ Alt KUBECONFIG"
  fi
}

# Function to decode and print all keys/values from a Kubernetes secret
kdec(){
   kubectl get secret $1 -o go-template='{{range $k,$v := .data}}{{printf "%s: " $k}}{{if not $v}}{{$v}}{{else}}{{$v | base64decode}}{{end}}{{"\n"}}{{end}}'
}

# k logs pod/dexcom-datadog-operator-d96ddf49d-ccrnh | jq 'select(.level == "ERROR" and .logger != "controllers.DatadogGenericResource")'
# kerror() {
#   k logs "$1" | jq 'select(.level == "ERROR")'
# }

argo-port-forward() {
  get-argo-secret

  # Check if port-forward to localhost:8082 is already running
  RUNNING_PID=$(pgrep -f "kubectl port-forward svc/argocd-platform-server 8082:80 -n argocd")
  if [[ -n "$RUNNING_PID" ]]; then
    echo "Port-forward to localhost:8082 is already runnin with PID ${RUNNING_PID}. Exiting."
    return 1
  fi

  # Run port-forward and open browser based on OS
  if [[ "$OSTYPE" == "darwin"* ]]; then
    k port-forward svc/argocd-platform-server 8082:80 -n argocd --context=$(kubectx -c)
    # sleep 2 && open http://localhost:8082
  else
    k port-forward svc/argocd-platform-server 8082:80 -n argocd --context=$(kubectx -c)
    # sleep 2 && xdg-open http://localhost:8082
  fi
}

# --- Source Additional Configs ---
for file in "$HOME/Projects/etc/api-keys.zshrc" "$HOME/Projects/etc/clark-more-zsh.zshrc" "$HOME/Projects/etc/cloudsql-aliases.zshrc"; do
  [[ -f $file ]] && source $file
done

# --- rbenv ---
eval "$(rbenv init -)"

# --- Prompt Customization ---
PROMPT='$(kube_ps1)$(kubeconfig-status)$(current-pulumi-stack) '$PROMPT
ip() {
  typeset -a ITERM_PROFILES=(
    # "Default"
    # "idleToes"
    "Fairyfloss"
    "ToyChest"
    "GruvboxDark"
    "Purple Rain"
    "FrontEndDelight"
    "Jackie Brown"
  )

  setopt KSH_ARRAYS 2>/dev/null
  local index=$((RANDOM % ${#ITERM_PROFILES[@]}))
  local random_profile=${ITERM_PROFILES[$index]}
  echo -e "\033]50;SetProfile=$random_profile\a"
  unsetopt KSH_ARRAYS 2>/dev/null
}
if [[ -o interactive && -n "${ITERM_SESSION_ID:-}" ]]; then
  ip
fi

# --- Miscellaneous ---
export TABSTOP=2
export CLOUDSDK_PYTHON_SITEPACKAGES=1

# --- zoxide (smarter "cd") ---
# Install: https://github.com/ajeetdsouza/zoxide
# Alternative: fzf (great for fuzzy history/file search, different workflow).
if command -v zoxide >/dev/null 2>&1; then
  eval "$(zoxide init zsh)"
fi

# --- End of zshrc ---